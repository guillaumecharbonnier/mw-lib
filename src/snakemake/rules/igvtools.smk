rule igvtools_index_idx:
    """
    Created:
        2019-01-23 15:15:15
    Aim:
        Creates an index for an alignment or feature file.
        Index files are required for loading alignment files into IGV, 
        and can significantly improve performance for large feature files. 
        Note that the index file is not directly loaded into IGV. 
        Rather, IGV looks for the index file when the alignment or feature file is loaded. 
        This command does not take an output file argument. 
        Instead, the filename is generated by appending '.sai' (for alignments) 
        or '.idx' (for features) to the input filename as IGV relies on 
        this naming convention to find the index . 
        The input file must be sorted by start position (see sort command, below).
    Test:
        out/igvtools/index_idx/igvtools/sort/ln/alias/experiments/hg38_hemato_seg_viz/HSC_1.bed.idx
    """
    input:
        "out/{filler}"
    output:
        ln  = "out/igvtools/index_idx/{filler}",
        idx = "out/igvtools/index_idx/{filler}.idx"
    conda:
        "../envs/igvtools.yaml"
    shell:
        "ln -srf {input} {output.ln} ; "
        "igvtools index {output.ln}"

rule igvtools_index_sai:
    """
    Created:
        2019-01-23 15:20:24
    Note:
        The 'sai' index is an IGV format, it does not work with samtools or any other application.
    Test:
    """
    input:
        "{filler}"
    output:
        "{filler}.sai"
    conda:
        "../envs/igvtools.yaml"
    shell:
        "igvtools index {input}"

rule igvtools_sort:
    """
    Created:
        2019-01-23 15:25:46
    Test:
        out/igvtools/sort/ln/alias/experiments/hg38_hemato_seg_viz/HSC_1.bed
    """
    input:
        "out/{filler}"
    output:
        "out/igvtools/sort/{filler}"
    conda:
        "../envs/igvtools.yaml"
    shell:
        "igvtools sort {input} {output}"


rule igvtools_tdftobedgraph:
    """
    Created:
        2020-09-30 22:28:38
    Test:
        out/igvtools/tdftobedgraph/ln/updir/mw-tall/inp/GSE60104/GSM1464990_20140211_733.spikein.hg19.bedgraph.BedGraph
    """
    input:
        "out/{filler}.tdf"
    output:
        "out/igvtools/tdftobedgraph/{filler}.BedGraph"
    conda:
        "../envs/igvtools.yaml"
    shell:
        "igvtools tdftobedgraph {input} {output}"



